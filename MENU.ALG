Algoritmo "API: Calculadora de SequÃªncias LÃ³gicas"

Var

   // Variaveis globais
   opcao: inteiro

   // Numeros primos
   n_pri, contador_pri, numero_pri, i_pri, qtd_divisores_pri, primo_pri : inteiro

   // Cubos perfeitos
   termo_cub, i_cub: inteiro
   valor_cub: real

   // Progressao geometrica
   iterador_geo, primeiro_termo_geo, numero_termos_geo, razao_geo, enesimo_termo_geo: inteiro

   // Sequencia alternada
   qtd_termos_alt, i_alt, razao_alt, opcao_alt: inteiro
   alt_sinal_alt, termo_alt: real

   // Sequencia de tribonacci
   termo_um_tri, termo_dois_tri, termo_tres_tri, termo_seguinte_tri, contador_tri: inteiro

   // Sequencia de fibonacci
   primeiro_termo_fib, segundo_termo_fib, proximo_termo_fib, contador_fib: inteiro

   // Sequencia de triangulos perfeitos
   n: real
   i, m, indice, m_base_calc: inteiro
   termos: vetor[1..51] de inteiro
   continuar: caractere

Inicio

   enquanto (opcao <> 7) faca

      escreval ("------MENU CALCULADORA-------")
      escreval ("")
      escreval (" 01 - Numeros Primos ")
      escreval (" 02 - SequÃªncia de Cubos Perfeitos ")
      escreval (" 03 - ProgressÃ£o GeomÃ©trica")
      escreval (" 04 - SequÃªncia Alternada")
      escreval (" 05 - SequÃªncia de Tribonacci")
      escreval (" 06 - SequÃªncia de Fibonacci")
      escreval (" 09 - Sequência de Triangulos Perfeitos")
      escreval (" 10 - Sair")
      escreval ("")
      escreval ("-----------------------------")
      leia (opcao)
      limpatela

      escolha (opcao)

      caso 1
         // Sequencia de numeros primos

         escreva ("Digite o valor de n (posiÃ§Ã£o do nÃºmero primo): ")
         leia (n_pri)
         escreval ("")

         contador_pri <- 0
         numero_pri <- 1

         enquanto contador_pri < n_pri faca
            numero_pri <- numero_pri + 1
            qtd_divisores_pri <- 0


            para i_pri de 1 ate numero_pri faca
               se numero_pri % i_pri = 0 entao
                  qtd_divisores_pri <- qtd_divisores_pri + 1
               fimse
            fimpara

            se qtd_divisores_pri = 2 entao
               contador_pri <- contador_pri + 1
               primo_pri <- numero_pri
            fimse

         fimenquanto

         escreval("O ", n_pri, "Âº nÃºmero primo Ã©:", primo_pri)
         escreval ("")

      caso 2
         // Sequencia de cubos perfeitos

         escreva ("Digite um termo da sequÃªncia de cubos perfeitoss:")
         leia (termo_cub)
         escreval ("")
         escreval ("Seu valor correspondente na sequÃªncia Ã©:", termo_cub ^ 3)

         para i_cub de 1 ate 50 faca

            valor_cub <- (termo_cub + i_cub) ^ 3
            escreval ("O termo", i_cub + termo_cub, " da sequÃªncia Ã©:", valor_cub)

         fimpara

         escreval ("")
         escreval ("SequÃªncia completa com", i_cub, " termos!")
         escreval ("")

      caso 3
         // Progressao geometrica

         escreva ("Digite o valor do primeiro termo:")
         leia (primeiro_termo_geo)
         escreval ("")

         escreva ("Digite o nÃºmero de termos da progressÃ£o:")
         leia (numero_termos_geo)
         escreval ("")

         escreva ("Digite o valor da razÃ£o:")
         leia (razao_geo)
         escreval ("")

         enesimo_termo_geo <- primeiro_termo_geo

         para iterador_geo de 1 ate numero_termos_geo faca
            escreval("Termo ", iterador_geo, " = ", enesimo_termo_geo)
            enesimo_termo_geo <- enesimo_termo_geo * razao_geo
         fimpara

         escreval ("")
         escreval ("ProgressÃ£o geomÃ©trica completa com", numero_termos_geo, " termos!")
         escreval ("")

      caso 4
         // Sequencia alternada

         escreval ("Bem vindo a funÃ§Ã£o de sequÃªncia alternada.")
         escreval ("")

         escreva ("Insira o valor do primeiro termo da sequÃªncia: ")
         leia (termo_alt)
         escreval ("")

         enquanto (termo_alt < 1) faca
            escreva ("Valor invÃ¡lido, digite novamente:")
            leia (termo_alt)
            escreval ("")
         fimenquanto

         escreva ("Insira tambÃ©m a razÃ£o da sequÃªncia: ")
         leia (razao_alt)
         escreval ("")

         enquanto (razao_alt <= 1) faca
            escreva ("Valor invÃ¡lido, digite novamente:")
            leia (razao_alt)
            escreval ("")
         fimenquanto

         razao_alt <- razao_alt * -1

         escreva ("Por Ãºltimo, insira a quantidade de termos da sequÃªncia: ")
         leia(qtd_termos_alt)
         escreval ("")

         enquanto (qtd_termos_alt <= 0) faca

            escreval ("NÃºmero invÃ¡lido, digite novamente.")
            leia (qtd_termos_alt)
            escreval ("")

         fimenquanto

         escreval ("Qual termo vocÃª deseja que seja positivo?")
         escreval ("")

         escreval ("Para o primeiro termo, digite 1.")
         escreval ("Para o segundo termo, digite 2.")
         escreval ("")

         leia (opcao_alt)
         escreval ("")

         enquanto (opcao_alt < 1) ou (opcao_alt > 2) faca
            escreva ("Valor invÃ¡lido, digite novamente:")
            leia (opcao_alt)
            escreval ("")
         fimenquanto

         escolha (opcao_alt)

         caso 1
            alt_sinal_alt <- 1
            termo_alt <- termo_alt * alt_sinal_alt

         caso 2
            alt_sinal_alt <- -1
            termo_alt <- termo_alt * alt_sinal_alt

         fimescolha

         escreval ("O termo 1 da sequÃªncia Ã©:", termo_alt)

         para i_alt de 1 ate qtd_termos_alt faca

            termo_alt <- (termo_alt * razao_alt)
            escreval ("O termo", i_alt + 1, " da sequÃªncia Ã©:", termo_alt)

         fimpara

         escreval ("")
         escreval ("SequÃªncia alternada completa com", qtd_termos_alt + 1, " termos!")
         escreval ("")

      caso 5
         //Sequencia de tribonacci

         escreva ("Digite o primeiro termo da sequÃªncia de Tribonacci: ")
         leia (termo_um_tri)

         termo_dois_tri <- 1
         termo_tres_tri <- 1

         escreval ("Sequencia de Tribonacci com primeiro termo ", termo_um_tri, ":")
         escreval ("Termo 1: ", termo_um_tri)
         escreval ("Termo 2: ", termo_dois_tri)
         escreval ("Termo 3: ", termo_tres_tri)

         para contador_tri de 4 ate 52 faca
            termo_seguinte_tri <- termo_um_tri + termo_dois_tri + termo_tres_tri
            escreval ("Termo", contador_tri, ": ", termo_seguinte_tri)

            termo_um_tri <- termo_dois_tri
            termo_dois_tri <- termo_tres_tri
            termo_tres_tri <- termo_seguinte_tri
         fimpara

         escreval ("")
         escreval ("SequÃªncia de Tribonacci completa com 52 termos!")

      caso 6
         // Sequencia de fibonacci

         // Solicita o primeiro termo ao usuÃ¡rio
         escreva ("Digite o primeiro termo da sequÃªncia de Fibonacci: ")
         leia (primeiro_termo_fib)

         // Define o segundo termo como 1 (padrÃ£o da sequÃªncia de Fibonacci)
         segundo_termo_fib <- 1

         // Exibe o cabeÃ§alho
         escreval ("SequÃªncia de Fibonacci com primeiro termo ", primeiro_termo_fib, ":")
         escreval ("Termo 1:  ", primeiro_termo_fib)
         escreval ("Termo 2:  ", segundo_termo_fib)

         // Calcula e exibe os prÃ³ximos 50 termos (termos 3 a 52)
         para contador_fib de 3 ate 52 faca
            proximo_termo_fib <- primeiro_termo_fib + segundo_termo_fib
            escreval ("Termo ", contador_fib, ": ", proximo_termo_fib)

            // Atualiza os valores para a prÃ³xima iteraÃ§Ã£o
            primeiro_termo_fib <- segundo_termo_fib
            segundo_termo_fib <- proximo_termo_fib
         fimpara

         escreval ("")
         escreval ("SequÃªncia de Fibonacci completa com 52 termos!")
         escreval ("")

      caso 9

         continuar <- "S"

         enquanto (continuar = "S") ou (continuar = "s") faca

            // Pede um número até ser válido
            repita
               escreva("Digite o termo (inteiro e positivo) que deseja calcular: ")
               leia(n)

               // Adiciona a validação: n deve ser positivo e igual à sua parte inteira
               se (n < 1) ou (n <> int(n)) entao
                  escreval("Valor inválido! Insira um número inteiro positivo.")
               fimse

            ate (n >= 1) e (n = int(n)) // Condição de saída: n é positivo e é inteiro

            // Converte n para inteiro para o resto do cálculo
            m_base_calc <- int(n)

            // Calcula o n-ésimo termo e os 50 seguintes
            escreval("")

            para indice de 0 ate 50 faca
               m <- m_base_calc + indice
               termos[indice + 1] <- (m * (m + 1)) div 2
            fimpara

            // Mostra o termo correspondente e os 50 seguintes
            escreval("O ", m_base_calc, "º termo da sequência de triângulos perfeitos é: ", termos[1])
            escreval("")
            escreval("Os 50 termos seguintes são:")

            para indice de 2 ate 51 faca
               escreval("T -", m_base_calc + indice - 1, " = ", termos[indice])
            fimpara

            escreval("")
            escreva("Deseja continuar a execução (S/N)? ")
            leia(continuar)
         fimenquanto

      caso 10

      fimescolha

   fimenquanto

Fimalgoritmo
